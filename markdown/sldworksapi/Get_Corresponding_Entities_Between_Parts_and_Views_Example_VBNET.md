---
title: "Get Corresponding Entities Between Parts and Drawing Views Example (VB.NET)"
project: ""
interface: ""
member: ""
kind: "example"
source: "sldworksapi/Get_Corresponding_Entities_Between_Parts_and_Views_Example_VBNET.htm"
---

# Get Corresponding Entities Between Parts and Drawing Views Example (VB.NET)

This example shows how to get corresponding entities or objects between a
part and its drawing.

```vb
  '----------------------------------------------------------------------------
 ' Preconditions:
  ' 1. Ensure that the specified part and drawing exist.
 ' 2. Open the Immediate window.
  ' 3. Uncomment the subroutine you want to run in Main().
  ' 4. At the pause, select a face, edge, vertex, feature, annotation,
 '    or sketch segment.
 ' 5. Press F5.
 '
 ' Postconditions:
 ' 1. Inspect the Immediate window.
  ' 2. If a corresponding face, edge, or vertex is found, it is selected in the
 '    underlying part or drawing.
 '
  ' NOTE: Because the models are used elsewhere, do not save changes.
  '----------------------------------------------------------------------------
 Imports SolidWorks.Interop.sldworks
 Imports SolidWorks.Interop.swconst

 Partial Class SolidWorksMacro

     Dim docSpec As DocumentSpecification
     Dim swModelPart As ModelDoc2
     Dim swModelDrawing As ModelDoc2
     Dim swDrawing As DrawingDoc
     Dim swView As View
     Dim lErr As Integer
     Dim selMgr As SelectionMgr
     Dim inputEntity As Entity
     Dim outputEntity As Entity
     Dim bSelected As Boolean
     Dim inputObject As Object
     Dim outputObject As Object
     Dim drComp As DrawingComponent

     Public Sub Main()

         'Uncomment the subroutine you want to run; comment the other one
         'ViewToPart()
         PartToView()

     End Sub
     Public Sub ViewToPart()

         docSpec = swApp.GetOpenDocSpec("C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\clamp1.SLDPRT")
         swModelPart = swApp.OpenDoc7(docSpec)

         docSpec = swApp.GetOpenDocSpec("C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\clamp1.SLDDRW")
         swModelDrawing = swApp.OpenDoc7(docSpec)

         swDrawing = swModelDrawing
         swView = swDrawing.FeatureByName("Drawing View1").GetSpecificFeature()

         swApp.ActivateDoc3(swModelPart.GetTitle, True, swRebuildOnActivation_e.swUserDecision, lErr)

         selMgr = swModelPart.SelectionManager
         swModelPart.ClearSelection2(True)

         Stop ' Select something in the model and press F5

         Select Case selMgr.GetSelectedObjectType3(1, -1)
             Case swSelectType_e.swSelFACES, swSelectType_e.swSelEDGES, swSelectType_e.swSelVERTICES

                 inputEntity = selMgr.GetSelectedObject6(1, -1)

                 Debug.Print("Using IView::GetCorrespondingEntity()")

                 outputEntity = swView.GetCorrespondingEntity(inputEntity)

                 If outputEntity Is Nothing Then
                     Debug.Print("No corresponding entity found in the drawing view")
                 Else
                     Debug.Print("Corresponding entity found....selecting in drawing")
                     swApp.ActivateDoc3(swModelDrawing.GetTitle, False, swRebuildOnActivation_e.swDontRebuildActiveDoc, lErr)
                     bSelected = outputEntity.Select4(False, Nothing)
                 End If

             Case swSelectType_e.swSelNOTHING

             Case Else

                 inputObject = selMgr.GetSelectedObject6(1, -1)

                 Debug.Print("Using IView::GetCorresponding()")

                 outputObject = swView.GetCorresponding(inputObject)

                 If outputObject Is Nothing Then
                     Debug.Print("No corresponding object found in the drawing view")
                 Else
                     Debug.Print("Corresponding object found in the drawing view")
                 End If
         End Select

     End Sub

     Public Sub PartToView()

         docSpec = swApp.GetOpenDocSpec("C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\clamp1.SLDPRT")
         swModelPart = swApp.OpenDoc7(docSpec)

         docSpec = swApp.GetOpenDocSpec("C:\Users\Public\Documents\SOLIDWORKS\SOLIDWORKS 2018\samples\tutorial\api\clamp1.SLDDRW")
         swModelDrawing = swApp.OpenDoc7(docSpec)

         swDrawing = swModelDrawing

         swApp.ActivateDoc3(swModelDrawing.GetTitle, False, swRebuildOnActivation_e.swDontRebuildActiveDoc, lErr)

         selMgr = swModelDrawing.SelectionManager
         swModelDrawing.ClearSelection2(True)

         Stop ' Select something in the drawing and press F5

         swView = swDrawing.FeatureByName("Drawing View1").GetSpecificFeature()

         Select Case selMgr.GetSelectedObjectType3(2, -1)
             Case swSelectType_e.swSelFACES, swSelectType_e.swSelEDGES, swSelectType_e.swSelVERTICES

                 inputEntity = selMgr.GetSelectedObject6(2, -1)
                 drComp = selMgr.GetSelectedObjectsComponent4(2, -1)

                 Debug.Print("Using IModelDocExtension::GetCorrespondingEntity2()")

                 outputEntity = swModelPart.Extension.GetCorrespondingEntity2(inputEntity)

                 If outputEntity Is Nothing Then
                     Debug.Print("No corresponding entity found in the part")
                 Else
                     Debug.Print("Corresponding entity found...selecting in part")
                     swApp.ActivateDoc3(swModelPart.GetTitle, False, swRebuildOnActivation_e.swDontRebuildActiveDoc, lErr)
                     bSelected = outputEntity.Select4(False, Nothing)
                 End If

             Case swSelectType_e.swSelNOTHING

             Case Else

                 inputObject = selMgr.GetSelectedObject6(2, -1)
                 drComp = selMgr.GetSelectedObjectsComponent4(2, -1)

                 Debug.Print("Using IModelDocExtension::GetCorresponding2()")

                 outputObject = swModelPart.Extension.GetCorresponding2(inputObject)

                 If outputObject Is Nothing Then
                     Debug.Print("No corresponding object found in the part")
                 Else
                     Debug.Print("Corresponding object found in the part")
                 End If
         End Select

     End Sub

     ''' <summary>
     ''' The SldWorks swApp variable is pre-assigned for you.
     ''' </summary>
     Public swApp As SldWorks

 End Class
```
